<?xml version="1.0" encoding="ISO-8859-1"?>
<!--
Copyright © 2002-2005 Curam Software Ltd.
All rights reserved.

This software is the confidential and proprietary information of Interactive
Technology Design, Ltd. ("Confidential Information"). You shall not disclose
such Confidential Information and shall use it only in accordance with the
terms of the license agreement you entered into with Curam Software.
-->
<!--
This stylesheet comes right after gen-jsp and applies various universal rules
to the output JSPs.
-->
<xsl:stylesheet version="1.0"
                xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:curam="http://www.curamsoftware.com/curam"
                xmlns:jsp="http://java.sun.com/JSP/Page"
                xmlns:xhtml="http://www.w3.org/1999/xhtml"
                exclude-result-prefixes="xhtml">

  <xsl:output method="xml" cdata-section-elements="jsp:text"/>
  
  <!--
  The prefix to use to locate static content such as images, etc.
  -->
  <xsl:param name="static-content-server-url" />

  <!--
  A flag indicating if preview pages are being processed. These are not JSP
  documents, so some handling is different.
  -->
  <xsl:param name="preview" select="'false'" />

  <!--
  This is the identity transformation.
  -->
  <xsl:template match="node()">
    <xsl:copy>
      <xsl:apply-templates select="@* | node()"/>
    </xsl:copy>
  </xsl:template>

  <xsl:template match="@*">
    <xsl:copy/>
  </xsl:template>
    
  <!--
  Outputs a copyright comment at the top of the JSP
  -->
  <xsl:template match="/">
    <xsl:comment>
Generated by IBM Curam JSP Code Generator. 

Licensed Materials - Property of IBM.  
Generator Copyright IBM Corporation 2002, 2012. All Rights Reserved.

US Government User Restricted Rights - Use, duplication 
restricted by GSA ADP Schedule Contract with IBM Corp. 
    </xsl:comment>
    <xsl:apply-templates/>
  </xsl:template>

  <!--
  Self-closing script/div tags cause problems for IE so force separate
  opening and closing tags by inserting a comment.
  -->
  <xsl:template match="xhtml:div[not(node())]">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:choose>
        <xsl:when test="$preview = 'false'">
          <jsp:text>&lt;!-- div content --&gt;</jsp:text>
        </xsl:when>
        <xsl:otherwise>
          <xsl:comment>div content</xsl:comment>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:copy>
  </xsl:template>

  <xsl:template match="xhtml:script[not(node())]">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:text>// script content</xsl:text>
    </xsl:copy>
  </xsl:template>

  <!-- Empty table header elements are rendered differently by IE so insert
       some content -->
  <xsl:template match="xhtml:th[not(node())]">
    <xsl:copy>
      <xsl:apply-templates select="@*"/>
      <xsl:text>&#160;</xsl:text>
    </xsl:copy>
  </xsl:template>

  <!--
  Wrap text nodes in jsp:text unless they are already wrapped in jsp:scriptlet
  or previews are being generated.
  -->
  <xsl:template match="text()">
    <xsl:choose>
      <xsl:when test="$preview = 'false'
                      and not(parent::jsp:scriptlet or parent::jsp:text)">
        <jsp:text>
          <xsl:value-of select="." disable-output-escaping="yes"/>
        </jsp:text>
      </xsl:when>
      <xsl:otherwise>
        <xsl:value-of select="." disable-output-escaping="no"/>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>
  
  <!--
  Prefix the static content URL to all "src" attributes. For preview pages,
  a fixed URL of ".." is always used.
  -->
  <xsl:template match="@src">
    <xsl:attribute name="src">
      <xsl:choose>
        <xsl:when test="$preview = 'false'">
          <xsl:value-of select="concat($static-content-server-url, .)"/>
        </xsl:when>
        <xsl:otherwise>
          <xsl:value-of select="concat('..', .)"/>
        </xsl:otherwise>
      </xsl:choose>
    </xsl:attribute>
  </xsl:template>
  
</xsl:stylesheet>
